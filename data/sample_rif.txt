.--------------
.- Definitions
.--------------

.define fade-in
	.to {"opacity": 0.0}
	.to {"opacity": 1.0}
.enddef

.define expand-and-fade-in
	.to {"font-size": "0%", "opacity": "0"}
	.to {"font-size": "100%"} .lasting 1000
	.to {"opacity": "1"} .lasting 1000
.enddef

.define fades-out-intro
	.animates #introfadefirst .to { "opacity": 0.2 } .lasting 3000 .to { "opacity": 0 } .lasting 15000
	.animates #introdream .to { "opacity": 1 } .lasting 2000 .to { "opacity": 0.2 } .lasting 4000 .to { "opacity": 0 } .lasting 30000
	.animates #introdream2 .to { "opacity": 1 } .lasting 2000 .to { "opacity": 0.2 } .lasting 4000 .to { "opacity": 0 } .lasting 20000
.enddef

.define hides-bar
	.animates #bar .to { "opacity": 0 }
.enddef
.define shows-bar
	.animates #bar .to { "opacity": 1 } .lasting 2000
.enddef

.define populates-icons
			.says <div class="baricon">{!<img src="image/look.png">|LOOK!}</div>
					<div class="baricon">{!<img src="image/move.png">|MOVE!}</div>
					<div class="baricon">{!<img src="image/use.png">|USE!}</div>
					<div class="baricon" style="margin-left:8px">{!<img src="image/talk.png">|TALK!}</div>
				.into #commandPane
.enddef

.---------------
.- World set up
.---------------

.set break=<span style='font-size:6pt;'>&nbsp;</span>

.move inner-door .to alcove
.move outer-door .to hallway

.move player .to titlescreen
.move mining-bot .to hallway

.-------------------------
.- Title screen responses
.-------------------------

.responses titlescreen
	.response
		.matches START
		.runs 1
		.does
			.hides-bar
			.says
				<div id="intro">
					<div id="introfadefirst">
						<div id="introrain"></div>
						<div id="introwind"></div>
					</div>
					<div id="introdream">{!You dream.|INTRODREAM!}<div id="introdream1"></div></div><div id="introdream2"></div>
				</div>
			.animates #intro .fade-in .lasting 1000
	.response
		.matches INTRODREAM
		.runs 2
		.does 1
				.says {!The patter of rain|INTRORAIN!}<div id="rainpunc">.</div> <div id="introrain2"></div> .into #introrain
				.says {!A caress of wind|INTROWIND!}<div id="windpunc">.</div> <div id="introwind2"></div> .into #introwind
				.says {!..|INTRODREAM!} .into #introdream1
				.animates #introrain, #introwind, #introdream1 .expand-and-fade-in
		.does 2 .says but you should not dream. .into #introdream2
				.animates #introdream2 .fade-in .lasting 1000
				.animates #introdream .to { "opacity": 1 } .lasting 1000
				.sets intro_saw_dream
				.calls INTRODONE

	.response
		.matches INTRORAIN 
		.runs 1
		.does
			.says , .into #rainpunc
			.says but there is no rain. .into #introrain2
			.animates #introrain2 .expand-and-fade-in
			.sets intro_saw_rain
			.invokes
				$("#introrain span").css("cursor", "inherit")
			.calls INTRODONE
	.response
		.matches INTROWIND
		.runs 1 
		.does
			.says , .into #windpunc
			.says but there is no wind. .into #introwind2
			.animates #introwind2 .expand-and-fade-in
			.sets intro_saw_wind
			.calls INTRODONE
		
	.response
		.matches INTRODONE
		.needs intro_saw_dream intro_saw_rain intro_saw_wind
		.does
			.fades-out-intro
			.populates-icons
			.shows-bar
			.moves player .to alcove
			.calls ENTER
.end

.-----------------
.- Door responses
.-----------------

.define door-base-responses
	.response
		.matches *door
		.groups
			.response
				.needs !door-open
				.does .says The alcove door is closed. .autohides
			.response
				.needs door-open
				.does .says The alcove door is open. .autohides
		.end
	.response
		.matches ALCOVE-DOOR-OPENS
		.does .says {!You trigger the door, and it slides open.|door!}
	.response
		.matches ALCOVE-DOOR-CLOSES
		.does .says {!You trigger the door, and it slides shut.|door!}
.enddef

.responses inner-door
	.door-base-responses
.end

.responses outer-door
	.door-base-responses
.end

.-------------------
.- Alcove responses
.-------------------

.responses alcove
	.response
		.matches ENTER
		.runs 1
		.does
			.says
				{=break=}
				<div id="wakeup">{!An incoming message wakens you.|message!} {!The conversations of the Others are muted and overlapping, but there's an unmistakable undercurrent of irritation.|theothers!}</div>
			.animates #wakeup .fade-in .lasting 3000

	.response
		.matches theothers
		.does
			.says 

	.response .matches EAR .does .says "Evaluate and repair." 

	.response
		.matches MOVE
		.prompts Exit your alcove
		.forcesprompt
		.does
			.uses first
				.response
					.needs door-open
					.does 
						.says Motors engage, wheels turn, and you slowly roll out of your alcove into the hallway.
						.moves player .to hallway
				.response
					.does .says {!You can't move while your alcove door is closed.|door!} .autohides
			.end
	.response
		.matches ALCOVE-DOOR-OPENS
		.orders 10
		.does .says As the door disappears into the wall, you discover that the hallway outside is crowded with small machines.
		Rather than streaming up and down the corridor in their usual work patterns, a large group of them is gathered around your door,
		watching and waiting intently for you.
.end

.-------------------
.- Player responses
.--------------------

.responses player
	.response
		.matches message
		.does
			.says
				The message is terse and to the point:<br>
				<span style="font-family:'Courier New'; font-size:12pt;">Directive, status immediate<br>
				M47-C communication disconnect. {!EAR!}.</span>

	.response
		.matches USE
		.does .says Use! .autohides

	.response
		.matches TALK
		.does .says Talk! .autohides

	.response
		.matches door=50
		.does
			.says The door is a distant memory now.
		
	.response
		.matches ALCOVE-DOOR-OPENS=10
		.does
			.says 1 {!You trigger the door to your alcove. It registers on your screen that it's now open.|door!}
			.says {!Your alcove door now registers as being open.|door!}

	.response
		.matches ALCOVE-DOOR-CLOSES=10
		.does
			.says 1 {!You trigger the door to your alcove. You  It registers on your screen that it's now closed.|door!}
			.says {!Your alcove door now registers as being closed.|door!}

	.response
		.matches *door
		.groups
			.response
				.needs !door-open
				.prompts Open the alcove door
				.forcesprompt
				.does
					.sets door-open
					.calls ALCOVE-DOOR-OPENS
			.response
				.needs door-open
				.prompts Close the alcove door
				.forcesprompt
				.does
					.sets !door-open
					.calls ALCOVE-DOOR-CLOSES
		.end

.end

.--------------------
.- Hallway responses
.--------------------
.responses hallway
	.response
		.matches LOOK
		.does .says You're in the hallway.
.end

.-----------------------
.- Mining bot responses
.-----------------------

.actions mining-bot
	.response
		.runs 1
		.needs door-open
		.does 
			.sets mining-bot.saw-player
			.says Spying you, one of the mining robots slowly slides forward.
.end

.responses mining-bot
.end

