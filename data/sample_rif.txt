.--------------
.- Definitions
.--------------

.define fade-in
	.to {"opacity": 0.0}
	.to {"opacity": 1.0}
.enddef

.define expand-and-fade-in
	.to {"font-size": "0%", "opacity": "0"}
	.to {"font-size": "100%"} .lasting 1000
	.to {"opacity": "1"} .lasting 1000
.enddef

.define fades-out-intro
	.animates #introfadefirst .to { "opacity": 0.2 } .lasting 3000 .to { "opacity": 0 } .lasting 15000
	.animates #introdream .to { "opacity": 1 } .lasting 2000 .to { "opacity": 0.2 } .lasting 4000 .to { "opacity": 0 } .lasting 30000
	.animates #introdream2 .to { "opacity": 1 } .lasting 2000 .to { "opacity": 0.2 } .lasting 4000 .to { "opacity": 0 } .lasting 20000
.enddef

.define hides-bar
	.animates #bar .to { "opacity": 0 }
.enddef

.define shows-bar
	.animates #bar .to { "opacity": 1 } .lasting 2000
.enddef

.define populates-icons
			.says <div class="baricon">{!<img src="image/look.png">|LOOK!}</div>
					<div class="baricon">{!<img src="image/move.png">|MOVE!}</div>
					<div class="baricon">{!<img src="image/use.png">|USE!}</div>
					<div class="baricon" style="margin-left:8px">{!<img src="image/talk.png">|TALK!}</div>
				.into #commandPane
.enddef


.---------------
.- World set up
.---------------

.set break=<span style='font-size:6pt;'>&nbsp;</span>

.move inner-door .to alcove
.move outer-door .to hallway

.move player .to titlescreen
.move mining-bot .to hallway

.move M47-C .to M47-C-Room
.move L17-C .to L17-C-Room

.include titlescreen.txt

.-----------------
.- Door responses
.-----------------

.define door-base-responses
	.response
		.matches *door
		.groups
			.response
				.needs !door-open
				.does .says The alcove door is closed. .autohides
			.response
				.needs door-open
				.does .says The alcove door is open. .autohides
		.end
	.response
		.matches ALCOVE-DOOR-OPENS
		.does .says {!You trigger the door, and it slides open.|door!}
	.response
		.matches ALCOVE-DOOR-CLOSES
		.does .says {!You trigger the door, and it slides shut.|door!}
.enddef

.responses inner-door
	.door-base-responses
.end

.responses outer-door
	.door-base-responses
.end

.-------------------
.- Alcove responses
.-------------------

.responses alcove
	.response
		.matches ENTER
		.occurs 1
		.does
			.says
				{=break=}
				<div id="wakeup">{!An incoming message wakens you.|message!} {!The conversations of the Others are muted and overlapping, but there's an unmistakable undercurrent of irritation.|theothers!}</div>
			.animates #wakeup .fade-in .lasting 3000

	.response
		.matches theothers
		.does
			.says 

	.response .matches EAR .does .says "Evaluate and repair." 

	.response
		.matches MOVE
		.prompts Exit your alcove
		.forcesprompt
		.does
			.uses first
				.response
					.needs door-open
					.does 
						.says Motors engage, wheels turn, and you slowly roll out of your alcove into the hallway.
						.moves-player-north-to hallway
				.response
					.does .says {!You can't move while your alcove door is closed.|door!} .autohides
			.end
	.response
		.matches ALCOVE-DOOR-OPENS
		.orders 10
		.does .says As the door disappears into the wall, you discover that the hallway outside is crowded with small machines.
		Rather than streaming up and down the corridor in their usual work patterns, a large group of them is gathered around your door,
		watching and waiting intently for you.
.end

.-------------------
.- Player responses
.--------------------

.responses player
	.response
		.matches message
		.does
			.says
				The message is terse and to the point:<br>
				<span style="font-family:'Courier New'; font-size:12pt;">Directive, status immediate<br>
				M47-C communication disconnect. {!EAR!}.</span>

	.response
		.matches USE
		.does .says Use! .autohides

	.response
		.matches TALK
		.does .says Talk! .autohides

	.response
		.matches door=50
		.does
			.says The door is a distant memory now.
		
	.response
		.matches ALCOVE-DOOR-OPENS=10
		.does
			.says 1 {!You trigger the door to your alcove. It registers on your screen that it's now open.|door!}
			.says {!Your alcove door now registers as being open.|door!}

	.response
		.matches ALCOVE-DOOR-CLOSES=10
		.does
			.says 1 {!You trigger the door to your alcove. It registers on your screen that it's now closed.|door!}
			.says {!Your alcove door now registers as being closed.|door!}

	.response
		.matches *door
		.groups
			.response
				.needs !door-open
				.prompts Open the alcove door
				.forcesprompt
				.does
					.sets door-open
					.calls ALCOVE-DOOR-OPENS

			.response
				.needs door-open
				.prompts Close the alcove door
				.forcesprompt
				.does
					.sets !door-open
					.calls ALCOVE-DOOR-CLOSES
		.end

.end

.--------------------
.- Hallway responses
.--------------------
.responses hallway
	.response
		.matches LOOK
		.does .says You're in the main corridor. {!One direction leads to the mining nexus|MOVE MINING_NEXUS!}, {!and the other leads toward the processing center and the controller rooms.|MOVE CONTROLLER_CORRIDOR!}
	.response
		.groups
			.response
				.matches MOVE MINING_NEXUS
				.forcesprompt
				.prompts Go down the corridor to the mining nexus
				.does 
					.says You move along the passage to the mining nexus.
					.moves-player-west-to mining nexus
			.response
				.matches MOVE CONTROLLER_CORRIDOR
				.forcesprompt
				.prompts Go up the corridor toward the processing center.
				.does 
					.says You head toward the processing center. The first stop along the corridor is a junction leading off
					to two rooms, each containing a controller.
					.moves-player-east-to controller corridor
		.end
.end

.--------------------------------
.- Controller corridor responses
.--------------------------------

.responses controller corridor
	.response
		.matches LOOK
		.does
			.says This is a section of corridor outside the controller rooms. 
	.response
		.groups
			.response
				.matches MOVE HALLWAY
				.forcesprompt
				.prompts Go up the corridor toward your alcove and the mining nexus
				.does 
					.says You move along the passage to the mining nexus.
					.moves-player-west-to hallway
			.response
				.matches MOVE PROCESSING_CENTER
				.forcesprompt
				.prompts Continue along the corridor to the processing center.
				.does 
					.says You enter the processing center. 
					.moves-player-east-to .to processing center
		.end
.end

.-------------------------
.- Mining nexus responses
.-------------------------

.responses mining nexus
.end

.-----------------------
.- Mining bot responses
.-----------------------

.actions mining-bot
	.response
		.occurs 1
		.needs door-open
		.does 
			.sets mining-bot.saw-player
			.says Spying you, one of the mining robots slides forward. "Help us." The voice drowns out the low-level chatter of the Others. "It's all stopped. Fix it."
.end

.responses mining-bot
.end

