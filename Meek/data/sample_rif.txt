.--------------
.- Definitions
.--------------

.define fade-in
	.to {"opacity": 0.0}
	.to {"opacity": 1.0}
.enddef

.define expand-and-fade-in
	.to {"font-size": "0%", "opacity": "0"}
	.to {"font-size": "100%"} .lasting 1000
	.to {"opacity": "1"} .lasting 1000
.enddef

.define hides-bar
	.animates #bar .to { "opacity": 0 }
.enddef

.define shows-bar
	.animates #bar .to { "opacity": 1 } .lasting 2000
.enddef

.define populates-icons
			.says <div class="baricon">{!<img src="image/look.png">|LOOK!}</div>
					<div class="baricon">{!<img src="image/move.png">|MOVE_ROSE!}</div>
					<div class="baricon">{!<img src="image/use.png">|USE!}</div>
					<div class="baricon" style="margin-left:8px">{!<img src="image/talk.png">|TALK!}</div>
				.into #commandPane
.enddef

.define moves-player-north-to .sets last-dir=north .moves player .to .<value> .enddef
.define moves-player-south-to .sets last-dir=south .moves player .to .<value> .enddef
.define moves-player-west-to  .sets last-dir=west  .moves player .to .<value> .enddef
.define moves-player-east-to  .sets last-dir=east  .moves player .to .<value> .enddef

.---------------
.- World set up
.---------------

.set break=<span style='font-size:6pt;'>&nbsp;</span>

.move inner-door .to alcove
.move outer-door .to hallway

.move player .to titlescreen
.move mining-bot .to hallway

.move M47-C .to M47-C-Room
.move L17-C .to L17-C-Room

.include titlescreen.txt

.-----------------
.- Door responses
.-----------------

.define door-open-close-response
	.response .<value>
		.groups
			.response
				.needs not door_open
				.prompts Open the alcove door
				.does
					.sets door_open
					.calls ALCOVE-DOOR-OPENS

			.response
				.needs door_open
				.prompts Close the alcove door
				.does
					.sets not door_open
					.calls ALCOVE-DOOR-CLOSES
		.end
.enddef

.responses door-base
	.response *door
		.groups
			.response
				.needs not door_open
				.does .says The alcove door is closed. .autohides
			.response
				.needs door_open
				.does .says The alcove door is open. .autohides
		.end
	.response ALCOVE-DOOR-OPENS
		.does .says {!You trigger the door, and it slides open.|door!}
	.response ALCOVE-DOOR-CLOSES
		.does .says {!You trigger the door, and it slides shut.|door!}
.end

.responses inner-door
	.reference door-base
.end

.responses outer-door
	.reference door-base
.end

.-------------------
.- Alcove responses
.-------------------

.responses alcove
	.response ENTER
		.occurs 1
		.does
			.says
				{=break=}
				<div id="wakeup">{!An incoming message wakens you.|message!} {!The conversations of the Others are muted and overlapping, but there's an unmistakable undercurrent of irritation.|theothers!}</div>
			.animates #wakeup .fade-in .lasting 3000

	.response theothers
		.does
			.says

	.response .matches EAR .does .says "Evaluate and repair."

	.response ALCOVE-EXIT-PROMPT
		.does .says Exit your alcove

	.response ROSE-NORTH
		.prompts {+ALCOVE-EXIT-PROMPT+}
		.does
			.uses first
				.response
					.needs door_open
					.does 
						.says Motors engage, wheels turn, and you slowly roll out of your alcove into the hallway.
						.moves-player-north-to hallway
				.response
					.does .says {!You can't move while your alcove door is closed.|door!} .autohides
			.end
	.response ALCOVE-DOOR-OPENS
		.orders 10
		.occurs 1
		.does .says As the door disappears into the wall, you discover that the hallway outside is crowded with small machines.
		Rather than streaming up and down the corridor in their usual work patterns, a large group of them is gathered around,
		watching and waiting intently for you.

	.door-open-close-response *USE
.end

.include player.txt

.--------------------
.- Hallway responses
.--------------------
.responses hallway
	.response LOOK
		.does .says You're in the main corridor. {!One direction leads to the mining nexus|MINING_NEXUS!}, {!and the other leads toward the controller rooms and the processing center.|CONTROLLER_CORRIDOR!}
	.response
		.groups
			.response MINING_NEXUS
				.does .says The mining nexus is a large cavern where the mining tunnels originate, leading off to underground depths. .autohides
			.response ROSE-WEST MINING_NEXUS
				.prompts Go to the mining nexus
				.does 
					.says You move along the passage to the mining nexus.
					.moves-player-west-to mining nexus
				.orders 2
			.response ROSE-EAST CONTROLLER_CORRIDOR
				.prompts Go toward the processing center
				.does 
					.says You head toward the processing center. The first stop along the corridor is a junction leading off
					to two rooms, each containing a controller.
					.moves-player-east-to controller corridor
				.orders 2
		.end
.end

.--------------------------------
.- Controller corridor responses
.--------------------------------

.responses controller corridor
	.response LOOK
		.does
			.says This is a section of corridor outside the controller rooms. 
	.response
		.groups
			.response ROSE-WEST HALLWAY
				.prompts Go up the corridor
				.does 
					.says You move along the passage to the mining nexus.
					.moves-player-west-to hallway
			.response ROSE-EAST PROCESSING_CENTER
				.prompts Go to the processing center
				.does 
					.says You enter the processing center. 
					.moves-player-east-to processing center
		.end
.end

.-------------------------
.- Mining nexus responses
.-------------------------

.responses mining nexus
.end

.include miningbot.txt
